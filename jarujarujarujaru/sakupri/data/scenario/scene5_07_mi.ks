*start

[cm]

;背景指定
[bg storage="summer.jpg" time="0"]

[message_init]

[layoff]

[fadeinbgm storage="../sound/se/semi.mp3" time="3000"]

[wait time="1000"]

[mask_off time="5000"]



[cg storage="summer.jpg"]

[wait time="1000"]

[layon]

8月。[p]

[wait time="1000"]

また夏が来た。[p]
大学生になってから、二度目の夏。[p]
一浪したから、高校を卒業してからは三度目の夏だ。[p]
毎年少しずつ暑くなっている気がするな……。[p]
じりじりと照りつける太陽に手をかざす。[p]
本来なら今頃夏休みを謳歌していたはずなのに、[r]今年から就活が前倒しになったせいで、[r]大学2年生にしてこうしてスーツを着ているというわけだ。[p]
このクソ暑い中スーツなんてどうかしてるぜ……。[p]

[wait time="1000"]

あなたのルーツはなんですか？[r]中学・高校時代を思い返してみましょう[p]

[wait time="1000"]

どの就活サイトにも載っている言葉。[p]
あの頃は何をしていたんだっけ。[p]
何かに熱中していたような気もするし、[r]ただ漫然と過ごしていた気もする。[p]
いずれにせよ、起業や留学をしているまわりの学生に比べれば、[r]大した思い出はない。[p]
ごく普通の、無気力な学生の一人だ。[p]

[wait time="1000"]

でも――[p]

[wait time="1000"]

あの頃を思い出そうとするたび、[r]心臓近くの動脈を小さな針が流れるような痛みを感じる。[p]



[fadeoutbgm]

[mask]

[bg storage="akita_shinkansen.jpg" time="0"]

[fadeinbgm storage="../sound/se/shinkansen.mp3" time="3000"]

[wait time="1000"]

[mask_off]



;【新幹線の中】[p]

面接のために、今日は新幹線に乗っている。[p]
仙台に本社がある商社だ。[p]
地方創生が進んだおかげで地方自治体に多額の予算が振られ、[r]地方に本社を構える企業が増えたとか。[p]
だから、去年くらいから地方での就活がトレンドになっている。[p]
俺は窓の外をぼんやりと眺める。[p]

[wait time="1000"]

あの時も、新幹線に乗ってどこか行ったっけ。[p]

[wait time="1000"]



;（新幹線のくだりフラッシュバック）[p]

[stopbgm]

[layoff]

[mask color="white" time="500"]

;ここで実際にキャラを回想で出そうと思ってたけど、澪と再会したときのインパクトを高めたいから出さない方向で行こうと思います。
[wait time="2000"]

[mask_off time="500"]



[wait time="1000"]

[playse storage="se/Chime-Announce05-1.mp3"]

[wait time="3000"]

[layon]

#
次は、大曲、大曲[p]

[wait time="1000"]

目を覚ますと、仙台をとっくに通り過ぎ、見知らぬ駅に着いていた。[p]
でも、なんとなく聞いたことのある名前だった。[p]

[wait time="500"]

……今更どうあがいても面接には間に合わない。[p]
俺は観念し、新幹線を降りた。[p]



[mask]

[bg storage="oomagari_yu.jpg" time="0"]

[wait time="1000"]

[mask_off]



[cg storage="oomagari_yu.jpg"]

;【大曲駅、改札前】[p]

改札を出ると、片田舎の駅とは思えないほどのすごい人の数だ。[p]
あたりを見回すと、駅舎に掲げられた花火大会の文字が。[p]

[wait time="1000"]

[playbgm storage="Esther_Abrami.mp3" volume="50"]

[wait time="1000"]

鮮明になっていく、4年前のあの日。[p]
あまりにもできすぎた偶然に導かれたことがどうにもこそばゆくて、[r]変な笑い方をしてしまう。[p]

[wait time="500"]

スマホの電源は切った。[p]
傾きかけた夕陽がまぶしい。[p]
浴衣のカップルや家族連れに紛れ、[r]一人スーツで河川敷の方へ歩いていく。[p]



[mask]

[bg storage="oomagari_kasen_yu.jpg" time="0"]

[wait time="1000"]

[mask_off]



[cg storage="oomagari_kasen_yu.jpg"]

;【河川敷】[p]
広大な河川敷を埋め尽くすビニールシートと立ち並ぶ屋台。[p]
その隙間を縫うように、あの場所を探す。[p]
4年前、時間が止まった、あの場所を。[p]

[wait time="1000"]

ぽつりぽつりと歩くたび、[r]過去にしがみつく己の惨めさが全身に突き刺さる。[p]
その痛みを、愚かさを、ようやく直視できた気がした。[p]
でも、それを受け容れられるほど、大人になってはいなかった。[p]

[wait time="1000"]

網膜を灼くような真っ赤な夕陽。[p]
この夕陽が沈んだら――[p]
俺は、何を見つめて生きていけばいいのだろう。[p]

[fadeoutbgm]

[wait time="1000"]

俺は――


;（澪との会話フラッシュバック）[p]
;ここどうしようかなあ。澪との思い出をバババって出すイメージ。
;一旦保留で。
;大人澪のインパクトを高めたいのでナシで

[wait time="2000"]
[er]

[playse storage="mio/mio5_52.wav"]
#？？？
ちょっと！そんなところに立たないでくださいよう。[p]

[wait time="1000"]

#
懐かしい声。[p]

[playse storage="mio/mio5_53.wav"]
#？？？
おーい！すいませーん！カメラの前に立たないでくださーい。[p]

#
カメラ――[p]

[wait time="500"]

はっ、と、振り返る。[p]
彼女は、三脚にカメラを構え、こちらを見つめていた。[p]



[layoff]

[mask color="white" time="500"]

[freeimage layer="base"]

[bg storage="white.png" time="0"]

[chara_cg name="mio" cg="099" _2="smile"]

[camera x="50" y="-300" zoom="2" time="0"]

[wait time="500"]

[mask_off time="2000"]



[wait time="1000"]

[camera x="0" y="-30" zoom="1" time="5000"]

[layon]

[playse storage="mio/mio5_54.wav"]
#mio
……光さん？[p]

#
偶然とか、運命とか、そんな大げさなものじゃない。[p]
彼女がいた。[p]
それだけのこと。[p]

[playse storage="mio/mio5_55.wav"]
#mio:smile
……こんなところでスーツ着てたら、ヤバイ奴だと思われますよ。[p]

[wait time="1000"]

#
あの時の明石さんがいた。[p]
それだけのことなのに、[p]
なぜか涙が止まらなかった。[p]



[layoff]

[mask time="3000"]

[freeimage layer="0"]
[reset_camera time="0"]

[cg storage="099"]

[bgmovie storage="hanabi.mp4"]

[playbgm storage="Esther_Abrami.mp3" volume="50"]

[wait time="1000"]

[mask_off]



*mio

[wait time="1000"]

[layon]



;【花火大会中】[p]

花火が上がる間、明石さんは仰々しい撮影機材をせわしく操り、[r]休む間なく写真を撮り続けた。[p]
話しかけていいものかわからず、[r]横でじっと彼女を見つめているほかなかった。[p]

[wait time="1000"]

でも、その姿はどこか頼もしくて、[r]4年前のあの日とは、少し違って見えた。[p]



[layoff]

[mask]

[stop_bgmovie time="0"]

[bg storage="oomagari_kasen_yoru.jpg" time="0"]

[chara_init name="mio" wear="black_skirt"]

[wait time="1000"]

[mask_off]



[cg storage="oomagari_kasen_yoru.jpg"]

;【花火終わり】[p]

[chara_show name="mio" top="-30" face="relieved"]

[layon]

[voconfig name="mio" vostorage="mio/mio5_{number}.wav" number="56"]

[vostart]

;#mioボイス
#mio
ふぅ〜。[p]

#
花火が終わり、ようやく一息ついたようだ。[p]

#hikaru
おつかれさま。[p]

;#mioボイス
#mio:default
ありがとうございます……何年ぶりですかね。[p]

#hikaru
4年……くらい？[p]

;#mioボイス
#mio:relieved
そっか……。[p]

#hikaru
……。[p]

;#mioボイス
#mio:default
……ねぇ、光さん。何か言うことがあるんじゃないですか？[p]

[vostop]

#hikaru
えーと……[wait time="1000"]髪、伸びたね。[p]

#mio:kimazui
……。[p]

#hikaru
じゃなくて……[wait time="1000"]長い髪も似合ってるね。[p]

[playse storage="mio/mio5_60.wav"]
;#mioボイス
#mio:relieved
……まあいいですけど。ふふっ、ありがとうございます。[p]

#
ずいぶん久しぶりで気が付かなかったが、[r]ショートヘアが肩のずっと下まで伸びていた。[p]
明石さんは少し照れたように、毛先を指でくるくるといじる。[p]

[wait time="1000"]

まわりのカップルや家族連れが帰路につく中、[r]俺と明石さんはこの4年間のことを語り合った。[p]
大学のこと、マーチングバンドのこと、[r]写真のこと、写真部のこと。[p]
マーチングバンドは高校卒業のタイミングで辞めてしまったが、[r]写真は大学でも続けているらしい。[p]

#mio:ah
葵さんと初音さんはお元気ですかね。[p]

#hikaru
葵は今でもたまに会うよ。まあ、あっちは彼氏とラブラブみたいだし前みたいには遊べないけどね。[p]

#mio:ahaha
へぇ〜。そんなに落ち込まないでください、光さん。[p]

#hikaru
別に落ち込んでないよ。[l]初音さんは卒業したきりだな。[p]

#mio:sad
そうでしたか。[p]

#
みんなの顔が浮かぶ。[p]
思えば、4年前の俺たちは、どこかぎこちなかった気がする。[p]
それが今、こうやって素直になれているのは時間のおかげだろうか。[l][r]それとも――[p]

[voconfig name="mio" vostorage="mio/mio5_{number}.wav" number="61"]

[vostart]

;#mioボイス
#mio:default
ねぇ、光さん。[p]

#hikaru
うん？[p]

;#mioボイス
#mio
私、ずっと光さんのこと好きでした。[p]

#hikaru
えっ。[p]

;#mioボイス
#mio:ahaha
えっ！なんですかそのリアクション！[p]

#hikaru
いや、急に来たからびっくりして。[p]

;#mioボイス
#mio:angry
ふん、わかってたくせに。そういうとこですよ、ほんと。[p]

#hikaru
あはは……。[p]

;#mioボイス
#mio:kimazui
この花火大会だって、[r]光さんに会えないかなって思って毎年来てたのに。[p]

#hikaru
えっ！？そうだったの？[p]

;#mioボイス
#mio:smile
そうですよ。意外と可愛いとこあるでしょ。へへ。[p]

[wait time="1000"]

#
何か気の利いたことを返すべきだろうが、うまく言葉が出てこない。[p]
黙り込んだ俺を見て、明石さんの表情が[ruby text="かげ"]翳る。[p]

[wait time="1000"]

;#mioボイス
#mio:ahaha
……あはは、なんかすいません。[r]じゃ、私、ホテルに戻らなきゃなんで。[p]

#
そう言うと手際よく荷物をまとめ、彼女は俺に背を向ける。[p]

[wait time="1000"]

――ああ、4年前もこうだった。[p]
言いたいことをはっきり言わず、涙や笑顔でごまかして。[p]
察してほしいと背中でわがままを言って。[p]
それなのにお互い気づかないフリをして。[p]
明石さんはちゃんと言ってくれた。[p]
俺は、素直にならなきゃいけない。[p]
彼女に、伝えなきゃいけない。[p]
どうしようもなく不器用な俺には、[r]これが精一杯だということを――。[p]

[fadeoutbgm]

[wait time="1000"]

俺は明石さんを、背中から力いっぱい抱きしめた。[p]



[layoff]

[mask color="white"]

[freeimage layer="0"]
[freeimage layer="base"]

[image storage="cg/cg100/cg100.jpg" width="1280" layer="0" visible="true" time="0"]

[camera y="-200" zoom="1.2" time="0"]

[wait time="500"]

[mask_off]



[camera y="-130" time="5000"]

[layon]

;#mioボイス
#mio
く、苦しいよ……。[p]

#
ギブアップとでも言いたげに俺の腕をぽんぽんと叩く。[p]

#hikaru
あ、ごめん……。[p]

#
どれだけの間こうしていたのだろう。[r]気づけば彼女を抱きしめる腕がビリビリと痺れていた。[p]
慌てて明石さんを解放する。[p]



[mask color="white" time="500"]

[freeimage layer="0"]
[reset_camera time="0"]

[cg storage="100"]

[bg storage="oomagari_kasen_yoru.jpg" time="0"]

[wait time="500"]

[mask_off time="500"]



[chara_show name="mio" top="-30" face="panic"]

;#mioボイス
#mio:panic
ふぇ〜……死ぬかと思った……。[p]

#hikaru
明石さん。[p]

;#mioボイス
#mio:angry
ちょっと待って。[p]

[vostop]

#hikaru
え？[p]

#mio:kimazui
……。[p]

#hikaru
……え？[p]

[voconfig name="mio" vostorage="mio/mio5_{number}.wav" number="71"]

[vostart]

;#mioボイス
#mio:angry
もーなんでわからないかなー。[p]

#hikaru
……スミマセン……。[p]

;#mioボイス
#mio:relieved
……明石さん、じゃなくて。[p]

#hikaru
……えっと……[wait time="500"]み、澪？[p]

[playbgm storage="Amazing_Grace-MB01-mp3/Amazing_Grace-MB01-1(Reverb-Slow).mp3" volume="70"]

;#mioボイス
#mio:doki
……んふ。[wait time="1000"]もっかい。[p]

#hikaru
澪。[p]

;#mioボイス
#mio:relieved
もっかい。[p]

#hikaru
澪。[p]

;#mioボイス
#mio:smile
もっかい。[p]

#hikaru
調子に乗るな。[p]

;#mioボイス
#mio:panic
あいて。[p]

軽くこつん、と叩いただけなのに[r]澪はオーバーリアクションで痛がる。[p]
ほんと、相変わらずだな。[p]
俺はすぅ、と深呼吸をする。[p]

[wait time="1000"]

#hikaru
澪。[p]

;#mioボイス
#mio:default
はい。[p]

#hikaru
正直……[wait time="500"]今でも、澪になんて言えばいいのかわからない。[p]

;#mioボイス
#mio:sad
……うん。[p]

#hikaru
好きって言うのも、なんか違う気がして。[p]

;#mioボイス
#mio
……そっか。[p]

#hikaru
でも、今こうしている間も胸がそわそわして。[p]
澪がまた遠くへ行っちゃうと思うと胸が苦しくて。[p]

[vostop]

#mio:ah
……。[p]

[wait time="1000"]

#hikaru
だから、できることなら、ずっとそばにいてほしい。[p]

[voconfig name="mio" vostorage="mio/mio5_{number}.wav" number="80"]

[vostart]

;#mioボイス
#mio:doki
……え？[p]

#
澪がきょとんとした顔で俺を見つめる。[p]
そんなに変なこと言ったか？[p]

#hikaru
……っていうのが今の俺の素直な気持ちなんだけど……[p]

;#mioボイス
#mio
その、それってつまり……[p]

#hikaru
え？[p]

;#mioボイス
#mio:default
ううん、なんでもない！[p]

#
と、澪は俺の胸元に顔をうずめ――[p]

;#mioボイス
#mio:doki
……普通に好きって言われるより100万倍うれしい。[p]

#hikaru
そうか……それならよかった。[p]

#
澪の小さな肩を、そっと抱いた。[p]

*star

[layoff]

[wait time="500"]

[image storage="../bgimage/star.jpg" layer="1" top="-180" width="1280" visible="true" time="5000" wait="false"]

[cg storage="star.jpg"]

[wait time="500"]

[camera y="180" time="20000" layer="1" wait="false"]

[wait time="1500"]

;黒背景に白文字にする。ほんとはopacityとかframeだけ変えればいいはずだけど一部の設定が変わっちゃうので、first.ksと同じ書き方にしている
[position frame="transparent.png" opacity="220"]
[position left="0" top="400" width="1280" height="320"]
[position margint="145" marginl="210" marginr="190" marginb="60"]
[font color="white"]
[anim name="chara_name_area" color="white" time="0"]

[layon]

夜空に瞬く無数の星。[p]
あの頃、俺たちを照らしていた夕陽はもう沈んでしまった。[p]
それなら、今は新しい明かりを探そう。[p]
今度は失くさないように、[p]
二人にそっと寄り添う明かりを。[p]

[layoff]

;メッセージレイヤ復活
[resetfont]
[position frame="white_gradient.png" opacity="220"]
[position left="0" top="400" width="1280" height="320"]
[position margint="145" marginl="210" marginr="190" marginb="60"]
[anim name="chara_name_area" color="black" time="0"]

[wait time="1000"]



[fadeoutbgm time="5000"]

[mask time="5000"]

[reset_camera time="0"]
[freeimage layer="0"]
[freeimage layer="1"]

[bg storage="brides_room.jpg" time="0"]

[chara_init name="aoi" wear="wedding"]
[chara_init name="mio" wear="wedding" jname="澪"]
[chara_init name="hatsune" wear="nijikai"]
[chara_init name="hikaru" jname="光"]

[wait time="3000"]

[mask_off time="3000"]



*wedding

[cg storage="brides_room.jpg"]

;（澪ルートエンディング）[p]
;【結婚式場のチャペル、控室】[p]

[wait time="1000"]

[layon]

#hikaru
遅いなぁ……。[p]

#
本番まであと5分だと言うのに、一体どこにいるんだ。[p]
スタッフの人も前代未聞の状況に色めき立っている。[p]
……まさか、ここに来て逃げ出したとか！？[p]
いや、あいつに限ってそれは……無いと信じたい。[p]

#スタッフ
万が一、新婦様が来られなかったら、御一人で始めちゃいましょう。[p]

#
なんじゃそりゃ。[p]
とはいえここまで来てゲストにやっぱり中止ですとも言えず、[r]やるしかないのか……。[p]



[layoff]

[mask]

[bg storage="chapel.jpg" time="0"]

[wait time="1000"]

[mask_off]



[cg storage="chapel.jpg"]

;【チャペル】[p]

[layon]

#司会
新郎の入場です。[p]

[fadeinbgm storage="218-Wagner-Lohengrin-Wedding-March.mp3" volume="50" sprite_time="7777-126000" time="100"]

[wait time="1000"]

#
なるべく凛々しい姿勢で。堂々と。[p]
神父さんのもとへ歩いていく。[p]

[wait time="1000"]

#司会
新婦様につきましては、ご都合によりご欠席とさせていただきます。[p]

;ざわ……ざわ
#
[wait time="1000"]

#
ゲストがざわめく。そりゃそうだ。[p]
これじゃまるで、俺と神父さんの結婚式だ。[p]
神父さんも苦笑いを浮かべている。[p]
ちらっと後ろを見ると、葵と初音さんも笑いを必死にこらえている。[p]

[chara_show name="aoi" top="-20" face="ahaha" time="0" page="back"]
[chara_show name="hatsune" top="-50" face="ahaha" time="0" page="back"]

[tr]

あーあ、これは一生バカにされること間違いなしだな。[p]

[fadeoutbgm]

と、その時――[p]
バーンという音とともに、チャペルの扉が開いた。[p]

[playse storage="se/metal_door.mp3" buf="1"]
[playse storage="se/impact.mp3"]



[layoff]

[mask color="white" time="2000"]

[freeimage layer="0"]

[image storage="cg/cg101/cg101.jpg" width="1280" layer="0" visible="true" time="0"]

[wait time="500"]

;カメラをあっためる
[camera x="0" y="0" zoom="1" time="0" layer="0"]
[camera x="-50" y="-200" zoom="1.5" time="0" layer="0"]

[mask_off time="2000"]



[camera x="0" y="-20" zoom="1" time="5000" layer="0"]

[layon]

;#mioボイス
#mio
すいませーん、おまたせしました！おっ、新郎、決まってるねぇ〜。はいこっち見て！ちょっと表情固いよ〜。[p]

[playbgm storage="church.mp3" volume="35"]

[wait time="1000"]

#
華やかなウェディングドレスにキラキラのティアラ。[p]
この世で一番美しい花嫁は、いかつい望遠レンズをこちらに向けて[r]シャッターを切りまくっている。[p]

[quake wait="false" time="500" vmax="30"]

#hikaru
何してんだバカ！！！[p]

;#mioボイス
#mio
光くん、大きい声出さないの。ここをどこだと思ってるのよ。[p]

[vostop]

#hikaru
こっちのセリフじゃあ！！！[p]

#
あーあ、もうめちゃくちゃだよ。[p]
ゲストが皆呆然とする中、[r]葵と初音さんは我慢を忘れひたすらに爆笑している。[p]

[freeimage layer="0" time="1000"]
[reset_camera time="0"]

[cg storage="101"]

[chara_show name="aoi" top="-20" face="excited" time="0" page="back"]
[chara_show name="hatsune" top="-50" face="excited" time="0" page="back"]

[tr]

[playse storage="aoi/aoi5_83.wav"]
;#aoiボイス
#aoi
あー、お腹痛い、死にそう。[p]

[playse storage="hatsune/hatsune5_34.wav"]
;#hatsuneボイス
#hatsune
ちょっと、お祝いなんだから、死ぬとか言わないの。ひひっ。[p]

#
こうなったらヤケクソだ。[p]

#hikaru
ほら、ちょっと来い！[p]

[chara_hide_all time="500"]

[chara_show name="mio" top="-20" face="doki"]

#mio
うわっ！ドレス崩れるから引っ張らないで！[p]

#
俺は澪をチャペルの前方へ引きずる。[p]

#hikaru
まったく、何やってんだよ。[p]

#mio:relieved
何って、光くんの勇姿を私が撮らずして誰が撮るのよ。[p]

#hikaru
ちゃんと専門のカメラマンがいるから！[p]

#mio:ah
えっ、そうなの？[p]

#
呆れて物が言えないという言葉を、[r]まさか結婚式で使うと思わなかった。[p]

#hikaru
……ったく、それに俺だけ映ってても意味ないだろ。[p]

#mio:ahaha
うーん、それもそうか。[p]

#
と、そこへ葵と初音さんがやって来る。[p]

[chara_show name="aoi" top="-20" face="ahaha"]
[chara_show name="hatsune" top="-50" face="relieved"]

[playse storage="aoi/aoi5_84.wav"]
;#aoiボイス
#aoi
ほら、澪ちゃんは主役なんだから。カメラ貸して。[p]

[playse storage="hatsune/hatsune5_35.wav"]
;#hatsuneボイス
#hatsune
ちゃんと私がきれいに撮ってあげるわよ。[p]

[voconfig name="mio" vostorage="mio/mio5_{number}.wav" number="86"]

[vostart]

;#mioボイス
#mio:angry
むー。壊さないでくださいね。大事な仕事道具なんですから。[p]

#
悪態をつきながらも澪は初音さんにカメラを渡す。[p]

[wait time="500"]

#神父
コホン、お二人、こちらへ。[p]

#
神父さんが呆れたように促す。[p]

#神父
だいぶ押してるから、巻きでいいかな。[p]

#hikaru
はい、もう、ほんとにすみません……。[p]

#神父
ふふっ、よろしい。では、誓いのキスを。[p]

[wait time="500"]

#
……ってそれは巻きすぎでは！？[p]
しかし横を見ると、澪はまんざらでもない顔をしている。[p]

;#mioボイス
#mio:default
……どうしたの、光くん？[p]



[fadeoutbgm]

[layoff]

[mask color="white" time="500"]

[freeimage layer="0"]

[image storage="cg/cg102/cg102.jpg" width="1280" layer="0" visible="true" time="0"]

[camera y="-180" time="0"]

[wait time="2000"]

[mask_off wait="false"]



[camera y="-20" time="20000" wait="false"]

[wait time="1000"]

[layon]

#
さっきまで式を破壊していた張本人が、[r]まるで関係ないと言わんばかりに純真な瞳でこちらを見つめる。[p]
やれやれ、だが残念なことに、[r]俺はその瞳に抵抗する術を持ち合わせていない。[p]
澪の見つめる彼方を、一生、[r]ともに追いかけていくと心に誓ったのだから。[p]
俺は彼女のベールをそっと持ち上げる。[p]

#hikaru
澪、愛してる。[p]

;#mioボイス
#mio
私も愛してる、光くん。[p]

[vostop]

#
そしてやさしく、キスをした。[p]

;（澪ルート、完！）[p]




[wait time="1000"]

[mask color="white" time="10000"]

[layoff]

[freeimage layer="0"]
[reset_camera time="0"]

[cg storage="102"]



;澪トゥルーエンドクリア
[eval exp="sf.true_count_mi += 1"]

;澪の壊れたタイトルタイム終了
[eval exp="sf.is_mio_time = false"]

@jump storage="title.ks"
